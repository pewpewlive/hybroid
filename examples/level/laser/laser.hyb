env Laser as Level

use Pewpew
use Fmath
use Globals
use ShipEntity

const LASER_DURATION = 13f
const HIT_COOLDOWN_MAX = 4

entity Laser {
    fixed timer = LASER_DURATION
    fixed scale

    bool isDead
    number color 
    number hitCooldown = 0

    fixed lx, ly
    fixed slope
    fixed x, y

    spawn(fixed x, y, lookToAngle, number color) {
        self.x, self.y = x, y
        self.color = color
        SetEntityMeshAngle(self, lookToAngle, 0f, 0f, 1f)
        SkipEntityMeshAttributesInterpolation(self)
        SetEntityMeshColor(self, color)
        ly, lx = Sincos(lookToAngle)
        slope = ly/lx

        let ax, ay = x, y
        if lx > 0f {
            ax = WIDTH-x
        }
        if ly > 0f {
            ay = HEIGHT-y
        }

        fixed a, b = Fmath:AbsFixed(ax/(lx*6f)), Fmath:AbsFixed(ay/(ly*6f))
        if b <= a {
            scale = b *6f
        }else {
            scale = a *6f
        }
        SetEntityMeshXYZScale(self, scale, 1f, 1f)
        SetEntityMesh(self, LaserMesh, 0)
        PlaySound(LaserSound, 0, x, y)
        SpawnEntity(self, 0)
    }

    destroy() {
        SetEntityUpdateCallback(self, fn(entity _){})
        isDead = true
        DestroyEntity(self)
    }

    Update() {
        hitCooldown -= 1
        timer -= 1f
        let t = FmathHelpers:Remap(1f, 0f, LASER_DURATION, 0f, timer)^2f
        SetEntityMeshXYZScale(self, scale, t, 1f)
        SetEntityMeshColor(self, ColorHelpers:LerpColors(0x00000000, self.color, t))
        if timer <= 0f {
            destroy self()
            return
        }
        if !IsEntityAlive(SHIP.id) return

        let px, py = GetEntityPosition(SHIP.id)

        let b1 = -slope*px+py
        let b2 = -slope*x+y

        let dist = Fmath:AbsFixed(b1-b2)/Fmath:Sqrt(1f+slope^2f)

        let playerVector = Fmath:Atan2(py-y, px-x)
        let laserVector = Fmath:Atan2(ly-y, lx-x)
        let diff = Fmath:AbsFixed(playerVector-laserVector)
        bool isInFront
        if diff < 90d {
            isInFront = true
        }else if diff > 270d {
            isInFront = true
        }

        if dist < 24f and timer > 8f and isInFront {
            CollideWithPlayer()
        }
    }

    fn CollideWithPlayer() {
        if !IsEntityAlive(self) return
        if !IsEntityAlive(SHIP.id) return
        if hitCooldown < 0 {
            DamageShip(SHIP.id, 1)
        }
        hitCooldown = HIT_COOLDOWN_MAX
    }
}